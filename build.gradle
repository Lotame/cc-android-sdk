buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.0'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'cc-android-sdk'
    bintrayName = 'cc-android-sdk'   // Has to be same as your library module name

    publishedGroupId = 'com.lotame'
    libraryName = 'LotameAndroidSDK'
    artifact = 'cc-android-sdk'     // Has to be same as your library module name

    libraryDescription = 'Lotame Platform Android SDK that enables sending data to Lotame from Android mobile apps.'

    // Your github repo link
    siteUrl = 'https://github.com/Lotame/cc-android-sdk'
    gitUrl = 'https://github.com/Lotame/cc-android-sdk.git'
    githubRepository= 'Lotame/cc-android-sdk'

    libraryVersionCode = 8
    libraryVersion = '2.8.0.0'

    pomVersion = '2.8.0.0'
    pomGroupID = 'com.lotame'

    developerId = 'Lotame'
    developerName = 'Lotame'
    developerEmail = 'info@lotame.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    namespace = "com.lotame.android"
    compileSdk 35
    buildToolsVersion = "35.0.1"

    defaultConfig {
        minSdk 14
        targetSdk 35
        versionCode project.ext.libraryVersionCode
        versionName "${project.ext.libraryVersion}"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

android.libraryVariants.configureEach { variant ->
    def name = variant.buildType.name

    project.tasks.register("jar${name.capitalize()}", Jar) {
        archiveFileName = "cc-android-sdk-${project.ext.libraryVersion}.jar"
        dependsOn variant.javaCompileProvider
        from variant.javaCompile.destinationDirectory
    }

    variant.outputs.all { file ->
        if (file.outputFileName.endsWith('.aar')) {
            file.outputFileName = "cc-android-sdk-${project.ext.libraryVersion}.aar"
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.7.0'
    api 'com.google.android.gms:play-services-ads-lite:23.6.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.2.0'
}

repositories {
    mavenCentral()
    google()
}

apply from: 'maven-install.gradle'