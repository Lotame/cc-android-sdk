buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'cc-android-sdk'
    bintrayName = 'cc-android-sdk'   // Has to be same as your library module name

    publishedGroupId = 'com.lotame'
    libraryName = 'LotameAndroidSDK'
    artifact = 'cc-android-sdk'     // Has to be same as your library module name

    libraryDescription = 'Lotame Platform Android SDK that enables sending data to Lotame from Android mobile apps.'

    // Your github repo link
    siteUrl = 'https://github.com/Lotame/cc-android-sdk'
    gitUrl = 'https://github.com/Lotame/cc-android-sdk.git'
    githubRepository= 'Lotame/cc-android-sdk'

    libraryVersionCode = 6
    libraryVersion = '2.4.0.0'

    pomVersion = '2.4.0.0'
    pomGroupID = 'com.lotame'

    developerId = 'Lotame'
    developerName = 'Lotame'
    developerEmail = 'info@lotame.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 29
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode project.ext.libraryVersionCode
        versionName "${project.ext.libraryVersion}"
    }
}

task jar(type: Jar) {
    archiveName "cc-android-sdk-${project.ext.libraryVersion}.jar"
    from android.sourceSets.main.java.srcDirs
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompileProvider
    task.from variant.javaCompile.destinationDirectory
    artifacts.add('archives', task)
}

dependencies {
    api 'com.android.support:appcompat-v7:25.3.1'
    api 'com.google.android.gms:play-services-ads:10.2.6'
}

repositories {
    mavenCentral()
    google()
}

apply from: 'maven-install.gradle'